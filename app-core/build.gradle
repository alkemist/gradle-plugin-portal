buildscript {
  repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenCentral()
  }
  dependencies {
    classpath "org.ratpack-framework.netty:ratpack-gradle:0.7.0-SNAPSHOT"
  }
}


apply plugin: "ratpack"
apply plugin: "groovy" // for testing

repositories {
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" }
}

mainClassName = "bootstrap.Main"


dependencies {
    springloaded "org.springsource.springloaded:springloaded-core:1.1.1"
    compile "org.apache.shiro:shiro-core:1.2.1"

    compile "com.h2database:h2:1.3.168"
    compile "org.apache.jena:jena-sdb:1.3.5", {
        exclude module: "slf4j-simple"
    }

    compile "com.google.code.gson:gson:2.2.2"

    compile "org.liquibase:liquibase-core:2.0.1"
    compile 'org.hibernate:hibernate-entitymanager:4.1.9.Final'
    compile 'org.hibernate:hibernate-c3p0:4.1.9.Final'
    compile 'com.google.code.guice-repository:guice-repository:2.0.2'
    compile 'org.springframework.data:spring-data-jpa:1.3.0.RELEASE'
}

idea.module { module ->
    sourceSets.whenObjectAdded {
        if (it.name.endsWith("Test")) {
            module.testSourceDirs += it.allSource.srcDirs.toList()
            module.scopes.TEST.plus += configurations[it.runtimeConfigurationName]
        } else {
            module.sourceDirs += it.allSource.srcDirs.toList()
            module.scopes.COMPILE.plus += configurations[it.compileConfigurationName]
            module.scopes.RUNTIME.plus += configurations[it.runtimeConfigurationName]
            module.scopes.RUNTIME.minus += configurations[it.compileConfigurationName]

        }
    }
}

apply from: "gradle/js-test.gradle"
apply from: "gradle/functional-test.gradle"
apply from: "gradle/dev.gradle"

