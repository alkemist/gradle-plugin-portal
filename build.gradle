buildscript {
  repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenCentral()
  }
  dependencies {
    classpath "org.ratpack-framework.netty:ratpack-gradle:0.7.0-SNAPSHOT"
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.4"
}

apply plugin: "ratpack"
apply plugin: "groovy" // for testing

repositories {
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" }
}

mainClassName = "bootstrap.Main"

apply plugin: "idea"

idea {
    project {
        jdkName "1.7"
        languageLevel "1.7"
    }
}

ext.deps = [
    groovy: "org.codehaus.groovy:groovy-all:2.1.1",
    spock: dependencies.create("org.spockframework:spock-core:0.7-groovy-2.0", { exclude module: "groovy-all" })
]

dependencies {
    springloaded "org.springsource.springloaded:springloaded-core:1.1.1"
    compile "org.apache.shiro:shiro-core:1.2.1"

    compile "com.h2database:h2:1.3.168"
    compile "org.apache.jena:jena-sdb:1.3.5", {
        exclude module: "slf4j-simple"
    }

    compile "com.google.code.gson:gson:2.2.2"
}

idea.module { module ->
    sourceSets.whenObjectAdded {
        if (it.name.endsWith("Test")) {
            module.testSourceDirs += it.allSource.srcDirs.toList()
            module.scopes.TEST.plus += configurations[it.runtimeConfigurationName]
        } else {
            module.sourceDirs += it.allSource.srcDirs.toList()
            module.scopes.COMPILE.plus += configurations[it.compileConfigurationName]
            module.scopes.RUNTIME.plus += configurations[it.runtimeConfigurationName]
            module.scopes.RUNTIME.minus += configurations[it.compileConfigurationName]

        }
    }
}

apply from: "gradle/js-test.gradle"
apply from: "gradle/functional-test.gradle"
apply from: "gradle/dev.gradle"

